/*
 * Copyright 2022 Phenix Real Time Solutions, Inc. Confidential and Proprietary. All rights reserved.
 */

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlinx-serialization'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.phenixrts.suite.groups"
        minSdk 21
        targetSdk 33
        versionCode 5
        versionName "2022.0"
        archivesBaseName = "PhenixGroups"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "int", "EXPIRATION_DAYS", "10"
        buildConfigField "int", "MAX_VIDEO_MEMBERS", "6"
        buildConfigField "long", "DATA_QUALITY_TIMEOUT_MS", "10000"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += [
                "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-Xuse-experimental=kotlinx.coroutines.ObsoleteCoroutinesApi",
                "-Xuse-experimental=kotlinx.coroutines.FlowPreview"]
    }
    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk true
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    buildToolsVersion '33.0.0'
    namespace 'com.phenixrts.suite.groups'
}

dependencies {
    // Phenix core and submodules
    implementation project(":phenixcore")
    implementation project(":phenixdeeplink")
    implementation project(":phenixdebugmenu")

    // Android
    api 'androidx.core:core-ktx:1.8.0'
    api 'androidx.appcompat:appcompat:1.5.0'
    api 'androidx.constraintlayout:constraintlayout:2.1.4'
    api 'com.google.android.material:material:1.6.1'
    api "androidx.lifecycle:lifecycle-extensions:2.2.0"
    api 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    api 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'

    // Kotlin
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    api 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.0'

    // Dagger
    kapt 'com.google.dagger:dagger-compiler:2.43.2'
    kapt 'com.google.dagger:dagger-android-processor:2.43.2'
    api 'com.google.dagger:dagger-android-support:2.43.2'

    // Timber
    api 'com.jakewharton.timber:timber:5.0.1'

    // Room components
    api 'androidx.room:room-runtime:2.4.3'
    api 'androidx.room:room-ktx:2.4.3'
    kapt 'androidx.room:room-compiler:2.4.3'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    testImplementation 'org.robolectric:robolectric:4.8.1'
    testImplementation 'androidx.test:core:1.4.0'

}
